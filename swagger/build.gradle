plugins {
    id 'org.springframework.boot' version "${springBootVersion}"
    id 'io.spring.dependency-management' version "${springDependencyMngmtVersion}"
    id 'java'
    id 'org.openapi.generator' version '7.19.0'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of("${javaVersion}")
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.15'
    implementation 'io.github.openfeign:feign-core:10.12'
    implementation 'io.github.openfeign:feign-okhttp:10.12'
    implementation 'io.github.openfeign:feign-jackson:10.12'
    implementation 'com.github.scribejava:scribejava-apis:8.3.3'
    implementation 'io.swagger.core.v3:swagger-annotations:2.2.40'
    implementation 'org.apache.oltu.oauth2:org.apache.oltu.oauth2.client:1.0.2'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

openApiGenerate {
    generatorName = "java"
    inputSpec = "$projectDir/openapi/petstore.yaml"
    outputDir = "$buildDir/openapi"
    configFile = "$projectDir/openapi/config-petstore.json"
    generateApiTests = false
    generateModelTests = false
    generateApiDocumentation = false
    generateModelDocumentation = false
}

compileJava {
    dependsOn tasks.named("openApiGenerate")
}

tasks.named("processResources") {
    dependsOn(tasks.named("openApiGenerate"))
}

sourceSets {
    main {
        java {
            srcDir "$buildDir/openapi/src/main/java"
        }
        resources {
            srcDir "$buildDir/openapi/src/main/resources"
        }
    }
}

test {
    useJUnitPlatform()
}
